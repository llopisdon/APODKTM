plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    def localProperties = new Properties()
    def localPropertiesFile = "local.properties"
    if (rootProject.file(localPropertiesFile).exists()) {
        localProperties.load(new FileInputStream(rootProject.file(localPropertiesFile)))
    }

    defaultConfig {
        applicationId "com.machineinteractive.apodktm"
        minSdkVersion 22
        targetSdkVersion 31
        versionCode 1
        versionName "1.1.7"

        def apodApiKey = localProperties["nasa_apod_api_key"] ?: 'DEMO_KEY'
        println("APOD KEY: $apodApiKey")
        buildConfigField("String", "APOD_API_KEY", "\"${apodApiKey}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def lifecycle_version = "2.4.0-alpha03"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"


    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def ktor_version = "1.6.0"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.1"

    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.2.1"

    implementation "io.coil-kt:coil:1.3.1"

    implementation 'com.github.Baseflow:PhotoView:2.3.0'
}
